plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version "6.3.1.5724"
    id 'jacoco'
}

group = 'pl.yalgrin'
version = '0.1.5-SNAPSHOT'

def vavrVersion = '0.10.7'
def spockVersion = '2.4-M6-groovy-4.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

bootJar {
    enabled = true
}

tasks.jar {
    enabled = false
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.springframework:spring-jdbc'
    implementation 'org.postgresql:postgresql'
    implementation 'org.postgresql:r2dbc-postgresql'
    implementation "io.vavr:vavr:$vavrVersion"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.testcontainers:r2dbc'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation "org.spockframework:spock-spring:$spockVersion"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()

    systemProperty "reactor.schedulers.defaultBoundedElasticOnVirtualThreads", "true"

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.setRequired(true)
    }
}